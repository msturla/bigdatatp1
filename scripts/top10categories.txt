REGISTER pk.jar;
register /home/hadoop/hbase-0.94.6.1/lib/protobuf-java-2.4.0a.jar;
data = LOAD 'pinkElephantTV/input/boxes.json' USING TextLoader() AS (line:chararray);
channel = LOAD 'hbase://channel' USING org.apache.pig.backend.hadoop.hbase.HBaseStorage('info:categories', '-loadKey true') AS (number:long, categories:chararray);
channel_categories = FOREACH channel GENERATE number, FLATTEN(TOKENIZE(categories)) as category:chararray;

tupledJson = FOREACH data GENERATE com.bigdata.pinkelephant.udf.JsonSplitter(line) as t;
flatJson = FOREACH tupledJson GENERATE FLATTEN(t);
channelLess = FILTER flatJson By channel != '';
projected = FOREACH channelLess GENERATE box_id, (int) channel;
joined = JOIN channel_categories BY number, projected by channel;

grouped = group joined BY category;

viewList = FOREACH grouped { box = joined.box_id; distinct_boxes = DISTINCT box; GENERATE group as cat, COUNT(distinct_boxes) as viewers;}

sorted = ORDER viewList BY viewers desc;
top10 = LIMIT sorted 10;


resultado:

(PPV,82)
(Adultos,59)
(Cine,36)
(General,35)
(Interes,35)
(Nacional,35)